shader BMFont : CharPlacementStruct
{
     SamplerState FontSampler
	{
		Filter = MIN_MAG_MIP_LINEAR;
		AddressU = Border;
		AddressV = Border;
	};


    // Gets the median of 3 values
    float median(float r, float g, float b) 
    {
        return max(min(r, g), min(max(r, g), b));
    }

    float RectSDF(float2 p, float2 b, float r)
	{
		float2 d = abs(p) - b + float2(r,r);
		return min(max(d.x, d.y), 0.0) + length(max(d, 0.0)) - r;   
	}

    stage float getBorder(float2 uv,float2 QuadSize, float radius, float borderThickness)
    {
        float2 pos   = uv*QuadSize;
        float  fDist = RectSDF(pos-QuadSize/2.0, QuadSize/2.0 - float2(borderThickness,borderThickness)/2, radius);

        return fDist * 2/borderThickness;
    }


    // Retrieves the pixel's Opacity sampled from a signed distance field font texture
    stage float getCharOpacity(float2 uv, StructuredBuffer<CharPlacementUV> CharPlacementUVBuffer, int startIndex, int endIndex, float screenPxRange, Texture2D FontTexture)
	{
		float opacity = 0;
		for (uint i=(uint)startIndex; i<(uint)endIndex;i++)
		{
            float2 destUV = uv * CharPlacementUVBuffer[i].DesSize - CharPlacementUVBuffer[i].DesPos;
            if (destUV.x > 0 && destUV.x < 1 && destUV.y > 0 && destUV.y < 1 )
            {
                float2 uv = destUV * CharPlacementUVBuffer[i].SrcSize + CharPlacementUVBuffer[i].SrcPos;
                float3 msd = FontTexture.SampleLevel(FontSampler, uv,0).rgb;
                float  sd  = median(msd.r, msd.g, msd.b);
                float screenPxDistance = screenPxRange*(sd - 0.5);
				opacity = max(opacity ,clamp(screenPxDistance + 0.5, 0.0, 1.0));

            }
        }
		return opacity;
	}
};
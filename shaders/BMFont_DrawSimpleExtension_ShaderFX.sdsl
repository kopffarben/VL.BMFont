shader BMFont_DrawSimpleExtension_ShaderFX : MaterialExtension
{
	compose BMFont_Provider Provider;

	stage stream float2 quadSize;
	stage stream float3 quadPlacement;
	stage stream float4 quadCol;
	stage stream float4 quadBGCol;

	// End of the VS (usually skinning)
	stage override void PreTransformPosition() {}

	// End of tessellation (usually displacement mapping in world space, etc...)
	stage override void TransformPosition() {}

	// At the end of the geometry pipeline (to generate ShadingPosition)
	stage override void PostTransformPosition()
	{
		streams.ShadingPosition = streams.Position; // must be assigned to something
	}

	static const float2 FontQuadUV[4] = {
        float2(0,0), 
        float2(1,0),
        float2(0,1),
        float2(1,1)
    };
	static const float3 FontQuadPositions[4] = {
        float3(-1, 1,0),
        float3( 1, 1,0),
        float3(-1,-1,0),
        float3( 1,-1,0),
    };

	// custom geometry shader, quad particle in this case
	[maxvertexcount(4)]
	stage void GSMain(point Input input[1], inout TriangleStream<Output> triangleStream)
	{
		streams = input[0];

		if (Provider.enabled())
		{
			// get data from BMFont_Provider implementation
			streams.PositionWS = float4(Provider.getQuadPos(), 1);
			streams.quadSize = Provider.getQuadSize();
			streams.quadPlacement = Provider.getPlacement();
			streams.quadCol = Provider.getQuadColor();
			streams.quadBGCol = Provider.getBGQuadColor();

			if (streams.quadSize.x > 0 || streams.quadSize.y > 0)
			{
				float4 posView = mul(streams.PositionWS, WorldView);
				for (int i=0; i<4; i++)
				{
					streams.TexCoord  = FontQuadUV[i].xy;
					float4 viewCornerPos = float4(posView.xyz + (FontQuadPositions[i].xyz + streams.quadPlacement) * float3(streams.quadSize,1), 1);
					streams.ShadingPosition = mul(viewCornerPos, Projection);

					triangleStream.Append(streams);
				}     
			}
		}
	}


	//override shading, create sphere impostor in this case
	stage override float4 Shading()
	{	
		// new code to write pos & depth
		// Shadows look correct now, but if you move the camera a distance away have other weirdness
		// /*
		float3 normal = float3(0,0,1);
		normal = normalize(mul(float4(normal, 0), ViewInverse).xyz);
		float4 worldPos = float4(streams.PositionWS.xyz, 1);
		// */




		float borderDist = Provider.getBorder(streams.TexCoord);

		float2 ddist1 = float2(ddx(borderDist-1), ddy(borderDist-1));
		float pixelDist1 = (borderDist-1) / length(ddist1);
		float border1 = saturate(0.5 - pixelDist1);

		float2 ddist2 = float2(ddx(borderDist), ddy(borderDist));
		float pixelDist2 = (borderDist) / length(ddist2);
		float border2 = saturate(0.5 - pixelDist2);

		float charDist = Provider.getCharOpacity(streams.TexCoord);
		float2 ddist3 = float2(ddx(charDist), ddy(charDist));
		float pixelDist3 = (charDist) / length(ddist3);
		float border3 = saturate(0.5 - pixelDist3);


		float4 colBorder1 = streams.quadCol   * border1;
		float4 colBorder2 = streams.quadBGCol * border2;
		float4 colBorder3 = streams.quadCol   * border2;
		float4 col		  = lerp(lerp(colBorder1,colBorder2,border2),colBorder3,charDist);

		if (border1 == 0 || (border2 == 1 && !Provider.useBG() && charDist == 0) ||  ( border2 < 1 && !Provider.useBorder())  )
		{
			discard;
		}

		return StrideShadingWorldNormal(worldPos, normal) * col;	
	}
};